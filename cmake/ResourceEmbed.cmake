function(embed_resources TARGET)
    cmake_parse_arguments(PARSE_ARGV 1 ARG "" "CATEGORY;NAMESPACE" "FILES")

    if(NOT ARG_CATEGORY)
        set(ARG_CATEGORY "Resources")
    endif()

    if(NOT ARG_NAMESPACE)
        set(ARG_NAMESPACE "ResourceData")
    endif()

    set(GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/${ARG_NAMESPACE}")
    file(MAKE_DIRECTORY "${GENERATED_DIR}")

    set(HEADER_FILE "${GENERATED_DIR}/${ARG_NAMESPACE}.h")
    set(ENTRIES_CPP "${GENERATED_DIR}/${ARG_NAMESPACE}.cpp")
    set(GENERATED_C_FILES "")
    set(ABSOLUTE_FILES "")

    set(FILE_INDEX 0)
    foreach(INPUT_FILE IN LISTS ARG_FILES)
        cmake_path(IS_ABSOLUTE INPUT_FILE IS_ABS)
        if(IS_ABS)
            set(ABSOLUTE_INPUT "${INPUT_FILE}")
        else()
            set(ABSOLUTE_INPUT "${CMAKE_CURRENT_SOURCE_DIR}/${INPUT_FILE}")
        endif()

        list(APPEND ABSOLUTE_FILES "${ABSOLUTE_INPUT}")
        list(APPEND GENERATED_C_FILES "${GENERATED_DIR}/BinaryResource${FILE_INDEX}.c")
        math(EXPR FILE_INDEX "${FILE_INDEX} + 1")
    endforeach()

    list(JOIN ABSOLUTE_FILES "," FILES_CSV)

    add_custom_command(
        OUTPUT ${GENERATED_C_FILES} "${ENTRIES_CPP}"
        COMMAND ResourceGenerator embed "${ENTRIES_CPP}" "${ARG_CATEGORY}" "${ARG_NAMESPACE}" "${FILES_CSV}"
        DEPENDS ${ABSOLUTE_FILES} ResourceGenerator
        COMMENT "Embedding ${ARG_CATEGORY} resources"
    )

    get_target_property(INIT_ADDED ${TARGET} RESOURCE_EMBED_INIT_ADDED)
    if(NOT INIT_ADDED)
        add_custom_command(
            OUTPUT "${HEADER_FILE}"
            COMMAND ResourceGenerator init "${HEADER_FILE}" "${ARG_NAMESPACE}"
            DEPENDS ResourceGenerator
            COMMENT "Generating ${ARG_NAMESPACE}.h"
        )
        target_sources(${TARGET} PRIVATE "${HEADER_FILE}")
        target_include_directories(${TARGET} PUBLIC "${GENERATED_DIR}")
        target_link_libraries(${TARGET} PUBLIC ResourceEmbedLib)
        set_target_properties(${TARGET} PROPERTIES RESOURCE_EMBED_INIT_ADDED TRUE)
    endif()

    target_sources(${TARGET} PRIVATE ${GENERATED_C_FILES} "${ENTRIES_CPP}")
endfunction()

function(embed_resource_directory TARGET)
    cmake_parse_arguments(PARSE_ARGV 1 ARG "" "CATEGORY;DIRECTORY;NAMESPACE" "")

    file(GLOB_RECURSE FILES "${ARG_DIRECTORY}/*.*")

    set(FORWARD_ARGS FILES ${FILES})
    if(DEFINED ARG_CATEGORY)
        list(APPEND FORWARD_ARGS CATEGORY "${ARG_CATEGORY}")
    endif()
    if(DEFINED ARG_NAMESPACE)
        list(APPEND FORWARD_ARGS NAMESPACE "${ARG_NAMESPACE}")
    endif()

    embed_resources(${TARGET} ${FORWARD_ARGS})
endfunction()
